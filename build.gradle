buildscript {
	repositories {
		// make it easy to test with a local goomph snapshot
		mavenLocal()
		// make it easy to test a snapshot version of goomph
		maven {	url 'https://oss.sonatype.org/content/repositories/snapshots/' }
		// grab dependencies from the gradle plugin portal
		maven { url 'https://plugins.gradle.org/m2/' }
	}
	// make sure we don't cache stale snapshot versions
	configurations.all {
		resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
	}
	dependencies {
		// a bunch of eclipse stuff
		classpath "com.diffplug.gradle:goomph:${VER_GOOMPH}"
		// creates a targetplatform
		classpath "org.standardout:bnd-platform:${VER_BND_PLATFORM}"
	}
}

///////////
// MAVEN //
///////////
subprojects {
	repositories {
		mavenCentral()
		// local eclipse maven (created by Goomph)
		maven {
			url rootProject.file('target.fromp2/build/goomph-p2asmaven/maven')
		}
	}
}

/////////////////////////////////////////
// TARGETPLATFORM (initialized lazily) //
/////////////////////////////////////////
project(':target.frommaven') {
	// the target platform will contain every jar
	// which is added to the platform config, as
	// well as all of its transitives, with OSGi
	// metadata created automatically by bnd as
	// required.
	configurations {
		platform
	}
}

/////////////
// PLUGINS //
/////////////
// tasks to clean and jar all of the plugins
task cleanPlugins	// clean all
task jarPlugins		// jar all
Closure IS_PLUGIN = { it.name.startsWith('org.eclipse.e4.demo.e4photo') || it.name.startsWith('needs') }
configure(subprojects.findAll(IS_PLUGIN)) {
	def PROJECT_NAME = it.name

	//////////
	// JAVA //
	//////////
	apply plugin: 'java'
	sourceSets {
		main { java {
			srcDir 'src'
		} }
		test { java {
			srcDir 'test'
		} }
	}
	sourceCompatibility = VER_JAVA
	targetCompatibility = VER_JAVA

	// add SWT and the appropriate platform-native SWT for building and testing
	dependencies {
		compile "eclipse-deps:org.eclipse.swt:+"
		compile "eclipse-deps:org.eclipse.swt.${com.diffplug.common.swt.os.SwtPlatform.getNative()}:+"
	}

	/////////////////////
	// TARGET PLATFORM //
	/////////////////////
	// the targetplatform depends on every project
	project(':target.frommaven').dependencies.add('platform', it)
	cleanPlugins.dependsOn(clean)
	jarPlugins.dependsOn(jar)
	task updateBundle {
		doLast {
			File pluginsDir = rootProject.file('targetplatform/build/plugins')
			// delete old versions of the plugin
			for (File plugin : pluginsDir.listFiles()) {
				if (plugin.name.startsWith(jar.baseName + "_") && plugin.name.endsWith('.jar')) {
					plugin.delete()
				}
			}
			// copy the most recent version
			org.apache.commons.io.FileUtils.copyFile(
				jar.archivePath, rootProject.file('targetplatform/build/plugins/' + jar.archiveName.replace('-', '_')));
		}
	}

	//////////
	// OSGI //
	//////////
	// create the manifest
	apply plugin: 'com.diffplug.gradle.osgi.bndmanifest'
	osgiBndManifest {
		copyTo 'META-INF/MANIFEST.MF'
	}
	// configure the OSGi bundle
	jar.manifest.attributes(
		'-exportcontents': 'org.eclipse.*',
		'-removeheaders': 'Bnd-LastModified,Bundle-Name,Created-By,Tool,Private-Package,Require-Capability',
		'Import-Package': '!javax.annotation.*,*',
		'Bundle-SymbolicName': project.name,
		'Bundle-RequiredExecutionEnvironment': 'J2SE-1.5',
	)

	//////////////////////
	// ECLIPSE PROJECTS //
	//////////////////////
	apply plugin: 'eclipse'
	// remove the build folder
	apply plugin: 'com.diffplug.gradle.eclipse.excludebuildfolder'
	// improve the project deps
	apply plugin: 'com.diffplug.gradle.eclipse.projectdeps'
	// handle build.properties correctly
	apply plugin: 'com.diffplug.gradle.eclipse.buildproperties'

	eclipse {
		project {
			natures 'org.eclipse.pde.PluginNature'
			natures 'org.eclipse.jdt.core.javanature'

			buildCommand 'org.eclipse.jdt.core.javabuilder'
			buildCommand 'org.eclipse.pde.ManifestBuilder'
			buildCommand 'org.eclipse.pde.SchemaBuilder'
		}
		classpath {
			downloadSources true
			downloadJavadoc true
		}
		jdt {
			sourceCompatibility VER_JAVA
			targetCompatibility VER_JAVA
		}
	}
	// always create "fresh" projects
	tasks.eclipse.dependsOn(cleanEclipse)
}

///////////////
// SETUP IDE //
///////////////
/*
apply plugin: 'com.diffplug.gradle.oomph.ide'
oomphIde {
	p2.addRepo(EclipseRelease.official('4.5.2').updateSite())
	p2.addRepo('http://download.eclipse.org/buildship/updates/e45/releases/1.0')
	p2.addIU('org.eclipse.sdk.ide')
	p2.addFeature('org.eclipse.buildship')

	targetplatform {
		installation('target.frommaven/build')
		installation('target.fromp2/build/goomph-p2asmaven/p2')
	}
}
*/
