def runtimeDeps = [
'org.eclipse.osgi',
'org.eclipse.update.configurator',
'org.eclipse.equinox.ds',
'org.eclipse.equinox.registry',
'org.eclipse.core.commands',
'javax.annotation',
'javax.inject',
'org.eclipse.core.jobs',
'org.eclipse.equinox.preferences',
'org.eclipse.core.contenttype',
'org.eclipse.equinox.util',
'org.eclipse.osgi.services'
]

dependencies {
	implementation project(':com.diffplug.needsBoth')
	implementation project(':com.diffplug.talks.rxjava_and_swt')

	implementation "eclipse-deps:com.ibm.icu:+"
	implementation "eclipse-deps:org.eclipse.osgi:+"
	implementation "eclipse-deps:org.eclipse.equinox.app:+"
	implementation "eclipse-deps:org.eclipse.equinox.common:+"
	implementation "eclipse-deps:org.eclipse.core.runtime:+"

	implementation "eclipse-deps:org.eclipse.swt:+"
	implementation "eclipse-deps:org.eclipse.jface:+"

	implementation "com.diffplug.durian:durian-core:${VER_DURIAN}"
	implementation "com.diffplug.durian:durian-collect:${VER_DURIAN}"
	implementation "com.diffplug.durian:durian-swt:${VER_DURIAN_SWT}"
	implementation "com.google.guava:guava:17.0"
	testImplementation "junit:junit:${VER_JUNIT}"

	runtimeOnly 'com.diffplug.osgi:com.diffplug.osgi.extension.sun.misc:0.0.0'
	runtimeDeps.each {
		runtimeOnly "eclipse-deps:${it}:+"
	}
}
jar.manifest.attributes('Bundle-SymbolicName': project.name + ';singleton:=true')

//////////////////////
// Run headless app //
//////////////////////
def codegenSetup = tasks.getByPath(':codegenSetup')
def thisLaunch = codegenSetup.launchTask(project, 'thisLaunch')
thisLaunch.args = ['-consoleLog', '-application', 'com.diffplug.rcpdemo.headlessapp', 'file', 'test']
