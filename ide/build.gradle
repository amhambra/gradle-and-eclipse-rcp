///////////////
// SETUP IDE //
///////////////
apply plugin: 'com.diffplug.gradle.oomph.ide'
oomphIde {
	splash '../_imgs/titlecard.png'
	pde {
		targetplatform {
			it.installation '../target.maven/build'
			it.installation '../target.p2/build/p2asmaven/p2runnable'
		}
	}
	style {
		classicTheme()	// oldschool cool
		niceText()		// nice fonts and visible whitespace
	}
	eclipseIni {
		vmargs('-Xmx2g')	// IDE can have 2 gigs of RAM, if it wants
	}

	addAllProjects()	// we want a project for *everything*

	// add syntax highlighting for gradle scripts
	p2.addRepo('http://www.nodeclipse.org/updates/gradle-ide-pack/')
	p2.addFeature('org.nodeclipse.enide.editors.gradle.feature')
}
// the IDE needs the targetplatform
ideSetupWorkspace.dependsOn(':target.maven:bundles')
